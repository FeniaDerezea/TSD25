model{

for(i in 1:ns){
	for(j in 1:2){

                n[i,j,1] <- N[i,j] 
		p[i,j,1] <- pr[i,j,1] 

		for(t in 2:Tc[i,j]){       # Note (1) (see below)
			n[i,j,t] <- x[i,j,t-1]  		
			p[i,j,t] <-  pr[i,j,t] / pr[i,j,t-1]	 
			}    
	}

	

	for(j in 1:2){
		for(t in 1:Tc[i,j]){
			x[i,j,t] ~ dbin(p[i,j,t], n[i,j,t])  	     # Likelihood                              
			d[i,j,t] <- (mu[i,j] - log(C[i,t]) ) / s[i,j] 
			logit(pr[i,j,t]) <- min(10, max(-10, d[i,j,t]) )   # Note (3)

			xhat[i,j,t] <- p[i,j,t]*n[i,j,t]  		# Fitted values

dev[i,j,t] <- 2*(x[i,j,t]*(log(x[i,j,t]) - log(xhat[i,j,t])) + (n[i,j,t] - x[i,j,t])*(log(n[i,j,t] - x[i,j,t])     
                                   - log(n[i,j,t] - xhat[i,j,t])))  # Residual deviance contribution
			}
		}

# Distributions of correlated random effects:
mu[i,1] ~ dnorm(mean[1], prec[1])
mu[i,2] ~ dnorm(mean[2], prec[2])

for(j in 1:2){
logs[i,j] ~ dnorm(mean[j+2], prec[j+2])
s[i,j] <- exp(logs[i,j])	
}

rd[i] <- sum(dev[i,1,1:Tc[i,1]]) + sum(dev[i,2,1:Tc[i,2]])	# Residual deviance study i

}

# Predictive distributions for random effects:
mupred[1] ~ dnorm(mean[1], prec[1])
mupred[2] ~ dnorm(mean[2], prec[2])
for(j in 1:2){	
	logspred[j] ~ dnorm(mean[2+j], prec[2+j])
}

# Priors:
for(r in 1:4){
	mean[r] ~ dnorm(0, 0.001)
	#sd[r] ~ dunif(0,5)
	prec[r] <- pow(sd[r], -2)
	}	
#informative priors based on OC-Sensor
sd[1]~dlnorm(0.2118, 14.75)#I(,3)
sd[2]~dlnorm(-1.9123,0.79)#I(,3)
sd[3]~dlnorm(-0.8517,14.93)#I(,1)
sd[4]~dlnorm(-2.0442, 1.12)#I(,1)


resdev <- sum(rd[])    # Total residual deviance
}
